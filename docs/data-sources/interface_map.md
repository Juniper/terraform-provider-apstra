---
page_title: "apstra_interface_map Data Source - terraform-provider-apstra"
subcategory: "Design"
description: |-
  This data source provides details of a specific Interface Map.
  At least one optional attribute is required.
---

# apstra_interface_map (Data Source)

This data source provides details of a specific Interface Map.

At least one optional attribute is required.


## Example Usage

```terraform
# This example reports on un-mapped interfaces found in various interface
# maps. These are cases where a particular switch model has more interfaces
# than are required for the fabric role to which it might be assigned (using
# a 48 port switch where the design only requires 12 ports, etc...)

# Grab a list of interesting Interface Map IDs. In this case we're
# interested only in interface maps which link hardware to the
# "AOS-7x10-Leaf" Logical Device design element.
data "apstra_interface_maps" "imaps" {
  logical_device_id = "AOS-7x10-Leaf"
}

# Loop over the matching Interface Map IDs and grab the full details of
# those objects so that we can inspect them further.
data "apstra_interface_map" "details" {
  for_each = data.apstra_interface_maps.imaps.ids
  id       = each.key
}

# Report stage: Loop over Interface Map objects, and then the interface
# mappings found inside. Filter the mappings for instances where the
# logical device panel and port are null (un-mapped). Output map
# (dictionary) where the key is Interface Map ID, and the value is the count
# of un-mapped interfaces.
output "unmapped_interface_count" {
  value = {
    for i in data.apstra_interface_map.details : i.id => length(
      [
        for j in i.interfaces : j if j.mapping.logical_device_panel == null && j.mapping.logical_device_panel_port == null
      ]
    )
  }
}

############################################################################
# The output looks like this:
#
#   unmapped_interface_count = {
#     "Arista_vEOS__AOS-7x10-Leaf" = 0
#     "Cisco_NXOSv__AOS-7x10-Leaf" = 2
#     "Cumulus_VX__AOS-7x10-Leaf" = 17
#     "Juniper_vQFX__AOS-7x10-Leaf" = 5
#     "VS_SONiC_BUZZNIK_PLUS__AOS-7x10-Leaf" = 49
#   }
############################################################################
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `id` (String) Apstra Interface Map ID. Required when `name` is omitted.
- `name` (String) Interface Map name displayed in the Apstra web UI. Required when `id` is omitted.

### Read-Only

- `device_profile_id` (String) ID of Device Profile referenced by this interface map.
- `interfaces` (Attributes Set) Detailed mapping of each physical interface to its role in the logical device (see [below for nested schema](#nestedatt--interfaces))
- `logical_device_id` (String) ID of Logical Device referenced by this interface map.

<a id="nestedatt--interfaces"></a>
### Nested Schema for `interfaces`

Read-Only:

- `active` (Boolean) Indicates whether the interface is used by the Interface Map
- `mapping` (Attributes) Mapping info for each physical interface (see [below for nested schema](#nestedatt--interfaces--mapping))
- `name` (String) Physical device interface name
- `position` (Number) todo - need to find out what this is
- `roles` (Set of String) Logical Device role ("connected to") of the interface.
- `setting` (String) Vendor specific commands needed to configure the interface, from the device profile.
- `speed` (String) Interface speed

<a id="nestedatt--interfaces--mapping"></a>
### Nested Schema for `interfaces.mapping`

Read-Only:

- `device_profile_interface_id` (Number) Port-specific interface ID from the device profile (used to identify interfaces in breakout scenarios.)
- `device_profile_port_id` (Number) Port number(ID) from the Device Profile.
- `device_profile_transformation_id` (Number) Port-specific transform ID from the Device Profile.
- `logical_device_panel` (Number) Panel number (first panel is 1) of the Logical Device port which corresponds to this interface.
- `logical_device_panel_port` (Number) Port number (first port is 1) of the Logical Device port which corresponds to this interface.
