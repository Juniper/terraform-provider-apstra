---
page_title: "apstra_datacenter_generic_system Resource - terraform-provider-apstra"
subcategory: ""
description: |-
  This resource creates a Generic System within a Datacenter Blueprint.
---

# apstra_datacenter_generic_system (Resource)

This resource creates a Generic System within a Datacenter Blueprint.

## Example Usage

```terraform
# This example uses the `apstra_datacenter_systems` data source and leaf
# switch tags to discover leaf switches in rack 11.
# It then provisions a 4x10G server with two 20G link aggregation groups

# We'll just pretend the blueprint ID is "abc"
locals {
  blueprint_id     = "abc"
  rack_11_leaf_ids = sort(tolist(data.apstra_datacenter_systems.rack_11_leafs.ids))
}

# The IDs field will include all systems matching the tag-based filter
data "apstra_datacenter_systems" "rack_11_leafs" {
  blueprint_id = local.blueprint_id
  filters = {
    tag_ids = ["leaf", "rack 11"]
  }
}

resource "apstra_datacenter_generic_system" "example" {
  blueprint_id      = local.blueprint_id
  label             = "Terraform Did This"
  hostname          = "terraformdidthis.example.com"
  tags              = ["terraform"]
  links = [
    {
      tags                          = ["10G", "terraform", "bond0"]
      lag_mode                      = "lacp_active"
      target_switch_id              = local.rack_11_leaf_ids[0] // first switch
      target_switch_if_name         = "xe-0/0/6"
      target_switch_if_transform_id = 1
      group_label                   = "bond0"
    },
    {
      tags                          = ["10G", "terraform", "bond0"]
      lag_mode                      = "lacp_active"
      target_switch_id              = local.rack_11_leaf_ids[1] // second switch
      target_switch_if_name         = "xe-0/0/6"
      target_switch_if_transform_id = 1
      group_label                   = "bond0"
    },
    {
      tags                          = ["10G", "terraform", "bond1"]
      lag_mode                      = "lacp_active"
      target_switch_id              = local.rack_11_leaf_ids[0] // first switch
      target_switch_if_name         = "xe-0/0/7"
      target_switch_if_transform_id = 1
      group_label                   = "bond1"
    },
    {
      tags                          = ["10G", "terraform", "bond1"]
      lag_mode                      = "lacp_active"
      target_switch_id              = local.rack_11_leaf_ids[1] // second switch
      target_switch_if_name         = "xe-0/0/7"
      target_switch_if_transform_id = 1
      group_label                   = "bond1"
    },
  ]
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `blueprint_id` (String) Apstra Blueprint ID.
- `links` (Attributes Set) Generic System link details (see [below for nested schema](#nestedatt--links))

### Optional

- `hostname` (String) System hostname.
- `label` (String) Name displayed in thw Apstra web UI.
- `tags` (Set of String) Tag labels to be applied to this Generic System. If a Tag doesn't exist in the Blueprint it will be created automatically.

### Read-Only

- `id` (String) Apstra graph node ID.

<a id="nestedatt--links"></a>
### Nested Schema for `links`

Required:

- `target_switch_id` (String) Graph Node ID of the Leaf Switch or Access Switch where the link connects.
- `target_switch_if_name` (String) Name of the physical interface where the link connects on the Leaf Switch or Access Switch ("ge-0/0/1" or similar).
- `target_switch_if_transform_id` (Number) Transformation ID sets the operational mode of an interface.

Optional:

- `group_label` (String) This field is used to collect multiple links into aggregation groups. For example, to create two LAG pairs from four physical links, you might use `group_label` value "bond0" on two links and "bond1" on the other two links. Apstra assigns a unique group ID to each link by default.
- `lag_mode` (String) LAG negotiation mode of the Link.
- `tags` (Set of String) Names of Tag to be applied to this Link. If a Tag doesn't exist in the Blueprint it will be created automatically.
